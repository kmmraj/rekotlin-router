apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka-android'
//apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "com.jfrog.bintray"
apply plugin: 'jacoco-android'
apply plugin: 'maven-publish'


group 'org.rekotlin-router'
version '0.1.6'


ext {
    junitPlugin_version = '1.0.0'
    junit_jupiter_version = '5.0.0'
    rekotlin_version = '0.1.0-SNAPSHOT'
    junit_version = "4.12"
    mockito_core_version = "1.10.19"
    assertj_core_version="3.6.2"
    android_support_lib_version = "25.4.0"
    powermock_module_junit4_version = "1.7.0RC2"
    powermock_api_mockito2_version = "1.7.0RC2"
    awaitility_version = "3.0.0"


    bintrayRepo = 'rekotlin-router'
    bintrayName = 'rekotlin-router'

    publishedGroupId = 'org.rekotlinrouter'
    libraryName = 'rekotlin-router'
    artifact = 'rekotlin-router'
    artifactName = 'org.rekotlinrouter:rekotlin-router'

    libraryDescription = 'A declarative router for ReKotlin. Allows developers to declare routes in a similar manner as URLs are used on the web.'

    siteUrl = 'https://github.com/kmmraj/rekotlin-router'
    gitUrl = 'https://github.com/kmmraj/rekotlin-router.git'

    libraryVersion = '0.1.0'

    developerId = 'developerId'
    developerName = 'Mohanraj K.M.'
    developerEmail = 'kmmraj@gmail.com'

    licenseName = 'MIT'
    licenseUrl = 'https://tldrlegal.com/license/mit-license'
    allLicenses = ["MIT"]
}

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.0"


    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "0.1.6"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        debug {
            //TODO: Keep it false until..
            // https://stackoverflow.com/questions/39195754/java-lang-noclassdeffounderror-failed-resolution-of-lorg-jacoco-agent-rt-inter
            testCoverageEnabled = false
        }
        release {
            minifyEnabled false
            testCoverageEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}


dependencies {
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation "com.android.support:appcompat-v7:$android_support_lib_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    compileOnly "tw.geothings.rekotlin:rekotlin:$rekotlin_version"
    testImplementation "tw.geothings.rekotlin:rekotlin:$rekotlin_version"
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.mockito:mockito-core:$mockito_core_version"
    testImplementation "org.assertj:assertj-core:$assertj_core_version"
    testImplementation "org.powermock:powermock-module-junit4:$powermock_module_junit4_version"
    testImplementation "org.powermock:powermock-api-mockito2:$powermock_api_mockito2_version"
    testImplementation "org.awaitility:awaitility:$awaitility_version"
}

def siteUrl = 'https://github.com/kmmraj/rekotlin-router'      // Homepage URL of the library
def gitUrl = 'https://github.com/kmmraj/rekotlin-router.git'   // Git repository URL
group = "org.rekotlinrouter"                                          // Maven Group ID for the artifact



task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
    inputs.dir 'src/main/java'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())



bintray {
    user = System.getenv("bintrayUser")
    key = System.getenv("bintrayApiKey")
    publications = ['rekotlinRouter']
    pkg {
        repo = libraryName
        name = libraryName
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = [licenseName]
        publish = true
        version {
            name = android.defaultConfig.versionName
            desc = libraryDescription
            released  = new Date()
        }
    }
}

jacoco {
    toolVersion = "0.7.6.201602180812"
    reportsDir = file("$buildDir/customJacocoReportDir")
}

publishing {
    publications {
        rekotlinRouter(MavenPublication) {
            groupId group
            artifactId 'rekotlin-router'
            version android.defaultConfig.versionName
            artifact(sourcesJar)
            artifact("$buildDir/outputs/aar/rekotlin-router-release.aar")
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.api.allDependencies.each { dependency ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependency.group)
                    dependencyNode.appendNode('artifactId', dependency.name)
                    dependencyNode.appendNode('version', dependency.version)
                }
            }
        }
    }
}
